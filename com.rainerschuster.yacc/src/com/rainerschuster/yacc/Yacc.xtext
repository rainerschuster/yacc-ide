grammar com.rainerschuster.yacc.Yacc //with org.eclipse.xtext.common.Terminals
 hidden(WS, ML_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate yacc "http://www.rainerschuster.com/yacc/Yacc"

Pec:
	{Pec} definitions+=Def* '%%' rules+=Rule* tail=Tail?
;

Tail:
	'%%' ANY_OTHER*//{ In this action, eat up the rest of the file }
;

Def:
	'%start' start=[Rule|ID]
	| {UnionDef}'%union' union=BLOCK /*{ Copy union definition to output }*/
	| CodeDef
	| /*definitions+=Def**/ rword=RWord tag=Tag? nlist+=Nmno (','? nlist+=Nmno)*
;

CodeDef:
	code=CODE /*{ Copy C code to output file }*/
;

RWord:
	'%token'
	| '%left'
	| '%right'
	| '%nonassoc'
	| '%type'
;

Tag:
	'<' Identifer '>'
;

Nmno:
	ref=Identifer /* NOTE: literal illegal with %type */
	| ref=Identifer number=INT /* NOTE: illegal with %type */
;

 /* rules section */

Rule:
	name=ID ':' alternatives+=RuleBody ('|' alternatives+=RuleBody)*
;

RuleBody:
	{RuleBody} rbody+=RuleBodyElement* prec=Prec?
;

RuleBodyElement:
	{RuleReference}ref=Identifer
 | Action
;

//Action : '{' /*{ Copy action, translate $$, etc. }*/ '}'
// ;

Action:
	action=BLOCK
;

Prec:
	{Prec} ('%prec' ref=Identifer action=Action?) ';'?
	| {Prec} ';'
;

Identifer:
	ID | LITERAL
;

terminal LITERAL:
	"'" ( '\\' . | !('\\'|"'") ) "'"
;

terminal BLOCK	: '{' -> '}';
terminal CODE	: '%{' -> '%}';


terminal ID  		: ('a'..'z'|'A'..'Z'|'_'|'.') ('a'..'z'|'A'..'Z'|'_'|'.'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
//terminal STRING	: 
//			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
//			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
//		; 
terminal ML_COMMENT	: '/*' -> '*/';
//terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
